plugins {
    id 'unmacaque.java'
    id 'org.springframework.boot'
    id 'io.spring.dependency-management'
}

configurations {
    integrationImplementation.extendsFrom testImplementation
    integrationRuntime.extendsFrom testRuntime
}

group = 'com.gmail.unmacaque.spring'
version = '1.0.0-SNAPSHOT'

repositories {
    maven { url = uri("https://repo.spring.io/release") }
    maven {
        url = uri("https://repo.spring.io/milestone")
        content {
            includeModule("org.springframework.security", "spring-security-bom")
        }
    }
}

springBoot {
    buildInfo {
        excludes = ['time']
    }
}

sourceSets {
    integration {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
        }
    }
}

task integrationTest(type: Test) {
    testClassesDirs = sourceSets.integration.output.classesDirs
    classpath = sourceSets.integration.runtimeClasspath
    useJUnitPlatform()
}
